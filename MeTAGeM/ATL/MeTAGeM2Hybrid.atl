--@atlcompiler atl2006
module MeTAGeM2Hybrid; -- Module Template
create OUT : MM_Hybrid from IN : AMW, left : MOF, right : MOF;

helper def : countRules : Integer = 1;

rule Module {
	from 
		amw : AMW!ModelRoot
	to
		hybrid : MM_Hybrid!Module (
			name_module <- amw.name.debug('Module '),
		    inMM <- amw.inputModel,
			outMM <- amw.outputModel,
			"rule" <- amw.relations			
		)
}

rule inModel{
	from
		in_MM_amw: AMW!InModelTransf
	to
		in_MM_hybrid: MM_Hybrid!InMetaModel(
			name_mm <-	in_MM_amw.name+'_model',
			type_mm <- in_MM_amw.name
		)
}

rule outModel{
	from
		out_MM_amw: AMW!OutModelTransf
	to
		out_MM_hybrid: MM_Hybrid!OutMetaModel(
			name_mm <-	out_MM_amw.name+'_model',
			type_mm <-  out_MM_amw.name
		)
}

rule InElement2SourceElementRuleWithoutGuard{
	from
		inElem: AMW!InElement (inElem.guardCondition.oclIsUndefined() and not( inElem.element.oclIsUndefined()))
	to
		sourceElem: MM_Hybrid!SourceElementRule(
			name_element <- inElem.name,
			metamodel <- inElem.element.modelRef
		)
}

rule InElement2SourceElementRuleWithGuard{
	from
		inElem: AMW!InElement (not inElem.guardCondition.oclIsUndefined() and not( inElem.element.oclIsUndefined()))
	to
		sourceElem: MM_Hybrid!SourceElementRule(
			name_element <- inElem.name,
			metamodel <- inElem.element.modelRef,
			guard <- sourceGuard
		),
		sourceGuard: MM_Hybrid!Guard(
			value <- inElem.guardCondition	
		)
}

rule OutElement2TargetElementRule{
	from
		outElem: AMW!OutElement
	to
		targetElem: MM_Hybrid!TargetElementRule(
			name_element <- outElem.name,
			metamodel <- outElem.element.modelRef,
			included <- outElem.relationsIncluded()
		)
}

helper context  AMW!OutElement  def : relationsIncluded () : Sequence(AMW!Relations) = 
	self.onetoOne.asSequence().union(self.manytoOne.asSequence().union(self.zerotoOne.asSequence()));

rule OneToOne2rule extends Relations2Rule{
	from
		relation: AMW!OneToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source.asSequence(),
			out <- relation.target.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}

rule OneToZero2rule extends Relations2Rule{
	from
		relation: AMW!OneToZero
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}

rule ZeroToOne2rule extends Relations2Rule{
	from
		relation: AMW!ZeroToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			out <- relation.target.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}

rule OneToMany2rule extends Relations2Rule{
	from
		relation: AMW!OneToMany
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source.asSequence(),
			out <- relation.target.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}
rule ManyToOne2rule extends Relations2Rule{
	from
		relation: AMW!ManyToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source.asSequence(),
			out <- relation.target.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}
rule ManyToMany2rule extends Relations2Rule{
	from
		relation: AMW!ManyToMany
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source.asSequence(),
			out <- relation.target.asSequence()
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}

abstract rule Relations2Rule{
	from
		relation:AMW!Relations(relation.isNotIncluded())
	to
		r_hybrid:MM_Hybrid!Rule(
			name_rule <- relation.getRuleName().debug('Rule '),
			isAbstract <- relation.typeRelation=#IsAbstract,
			isMain <- relation.typeRelation=#IsMain,
			"extends" <- relation.extend,
			isExtended <- relation.isExtend, 
			typeAttribute <- relation.typeAttri,
			typeElement <- relation.typeE
		)
}


rule OneToOne2ElementIncluded extends Relations2ElementIncluded{
	from
		relation_in:AMW!OneToOne
	to
		elementIncluded: MM_Hybrid!ElementIncluded (
			right <- rightPattern,
			left <- leftPattern
		),
		rightPattern:MM_Hybrid!RightPattern(
			sourceElement <- relation_in.source.asSequence(),
			"rule" <- relation_in.getRuleInvoked()
		),
		leftPattern:MM_Hybrid!LeftPattern(
			targetElement <- relation_in.target
		)
}

rule ManyToOne2ElementIncluded extends Relations2ElementIncluded{
	from
		relation_in:AMW!ManyToOne
	to
		elementIncluded: MM_Hybrid!ElementIncluded (
			right <- rightPattern,
			left <- leftPattern
		),
		rightPattern:MM_Hybrid!RightPattern(
			sourceElement <- relation_in.source.asSequence(),
			"rule" <- relation_in.getRuleInvoked()
		),
		leftPattern:MM_Hybrid!LeftPattern(
			targetElement <- relation_in.target
		)
}

rule ZeroToOne2ElementIncluded extends Relations2ElementIncluded{
	from
		relation_in:AMW!ZeroToOne
	to
		elementIncluded: MM_Hybrid!ElementIncluded (
			right <- rightPattern,
			left <- leftPattern
		),
		rightPattern:MM_Hybrid!RightPattern(

		),
		leftPattern:MM_Hybrid!LeftPattern(
			targetElement <- relation_in.target
		)
}


abstract rule Relations2ElementIncluded{
	from
		relation_in:AMW!Relations(relation_in.isIncluded())
	to
		elementIncluded:MM_Hybrid!ElementIncluded(
			typeAttribute <- relation_in.typeAttri,
			typeElement <- relation_in.typeE
		)
}

helper context AMW!Relations def: getRuleInvoked(): Sequence(AMW!Relations)=
	if(self.source.asSequence().size()>0) then
		--self.source.invoked.asSequence()
		self.source.asSequence()->collect(e|e.invoked).asSequence()
	else 
		Sequence{}
	endif;

helper context AMW!Relations def: isNotIncluded() : Boolean =
	not self.isIncluded();

helper context AMW!Relations def: isIncluded() : Boolean =
	if ((self.oclIsTypeOf(AMW!OneToOne))  or  (self.oclIsTypeOf(AMW!ManyToOne)) or (self.oclIsTypeOf(AMW!ZeroToOne))) then
		self.outElement.asSequence().size()>0
	else
		false
	endif;

helper context AMW!Relations def : getRuleName () : String = 
	if self.name.oclIsUndefined() then
		self.getInOutPatternName()
	else
		if self.name = '' then
			self.getInOutPatternName()
		else
			self.name
		endif
	endif;

helper context AMW!Relations def : getInOutPatternName () : String =
	if ((self.oclIsTypeOf(AMW!ZeroToOne))  or  (self.oclIsTypeOf(AMW!OneToZero))) then
		'R' + thisModule.countRules.toString()
	else
	if not self.source.asSequence()->first().oclIsUndefined() and not self.target.asSequence()->first().oclIsUndefined() then
		self.source.asSequence()->first().name + '_2_'+ self.target.asSequence()->first().name
	else
		'R' + thisModule.countRules.toString()
	endif
	endif;
