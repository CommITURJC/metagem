--@atlcompiler atl2006
module MeTAGeM2Hybrid; -- Module Template
create OUT : MM_Hybrid from IN : AMW, left : MOF, right : MOF;

helper def : countRules : Integer = 1;

rule Module {
	from 
		amw : AMW!ModelRoot
	to
		hybrid : MM_Hybrid!Module (
			name_module <- amw.name.debug('nombremodulo '),
		    inMM <- amw.inputModel,
			outMM <- amw.outputModel,	
			"rule" <- amw.relations			
		)
}

rule inModel{
	from
		in_MM_amw: AMW!InModelTransf
	to
		in_MM_hybrid: MM_Hybrid!InMetaModel(
			name_mm <-	in_MM_amw.name
		)
}

rule outModel{
	from
		out_MM_amw: AMW!OutModelTransf
	to
		out_MM_hybrid: MM_Hybrid!OutMetaModel(
			name_mm <-	out_MM_amw.name
		)
}

rule InElement2SourceElementRuleWithoutCondition{
	from
		inElem: AMW!InElement (inElem.guardCondition.oclIsUndefined())
	to
		sourceElem: MM_Hybrid!SourceElementRule(
			name_element <- inElem.name			
		)
}

rule InElement2SourceElementRuleWithCondition{
	from
		inElem: AMW!InElement (not inElem.guardCondition.oclIsUndefined())
	to
		sourceElem: MM_Hybrid!SourceElementRule(
			name_element <- inElem.name,
			condition <- sourceCondition
		),
		sourceCondition: MM_Hybrid!Condition(
			value <- inElem.guardCondition	
		)
}

rule OutElement2TargetElementRule{
	from
		outElem: AMW!OutElement
	to
		targetElem: MM_Hybrid!TargetElementRule(
			name_element <- outElem.name
		)
}


rule OneToOne2rule extends Relations2Rule{
	from
		relation: AMW!OneToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source,
			out <- relation.target
		)
}

rule OneToZero2rule extends Relations2Rule{
	from
		relation: AMW!OneToZero
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source
		)
}

rule ZeroToOne2rule extends Relations2Rule{
	from
		relation: AMW!ZeroToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			out <- relation.target
		)
}

rule OneToMany2rule extends Relations2Rule{
	from
		relation: AMW!OneToMany
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source,
			out <- relation.target
		)
}
rule ManyToOne2rule extends Relations2Rule{
	from
		relation: AMW!ManyToOne
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source,
			out <- relation.target
		)
}
rule ManyToMany2rule extends Relations2Rule{
	from
		relation: AMW!ManyToMany
	to
		r_hybrid: MM_Hybrid!Rule(
			"in" <- relation.source,
			out <- relation.target
		)
}


abstract rule Relations2Rule{
	from
		relation:AMW!Relations
	to
		r_hybrid:MM_Hybrid!Rule(
			--name_rule <- relation.getRuleName().debug('nombre rule '),
			name_rule <- 'R' + thisModule.countRules,
			isAbstract <- relation.isAbstract,
			isMain <- relation.isMain,
			typeAttribute <- relation.typeAttri,
			typeElement <- relation.typeE
		)
	do {
		thisModule.countRules <- thisModule.countRules + 1;
	}
}

helper context AMW!Relations def : getRuleName () : String = 
	if self.name.oclIsUndefined() then
		self.getInOutPatternName()
	else
		if self.name = '' then
			self.getInOutPatternName()
		else
			self.name
		endif
	endif;

helper context AMW!Relations def : getInOutPatternName () : String =
	if not self.source.oclIsUndefined() and not self.target.oclIsUndefined() then
		self.in.name_element + '_2_'+ self.out->select ( e | true )->first().name_element
	else
		'R' + thisModule.countRules
	endif;
